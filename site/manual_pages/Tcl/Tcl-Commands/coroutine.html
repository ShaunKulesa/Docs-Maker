<h1 id="name">NAME</h1>
<p>coroutine, yield, yieldto - Create and produce values from
coroutines</p>
<h1 id="synopsis">SYNOPSIS</h1>
<pre><code>coroutine name command ?arg...?
yield ?value?
yieldto command ?arg...?
name ?value...?</code></pre>
<h1 id="description">DESCRIPTION</h1>
<p>The <strong>coroutine</strong> command creates a new coroutine
context (with associated command) named <em>name</em> and executes that
context by calling <em>command</em>, passing in the other remaining
arguments without further interpretation. Once <em>command</em> returns
normally or with an exception (e.g., an error) the coroutine context
<em>name</em> is deleted.</p>
<p>Within the context, values may be generated as results by using the
<strong>yield</strong> command; if no <em>value</em> is supplied, the
empty string is used. When that is called, the context will suspend
execution and the <strong>coroutine</strong> command will return the
argument to <strong>yield</strong>. The execution of the context can
then be resumed by calling the context command, optionally passing in
the <em>single</em> value to use as the result of the
<strong>yield</strong> call that caused the context to be suspended. If
the coroutine context never yields and instead returns conventionally,
the result of the <strong>coroutine</strong> command will be the result
of the evaluation of the context.</p>
<p>The coroutine may also suspend its execution by use of the
<strong>yieldto</strong> command, which instead of returning, cedes
execution to some command called <em>command</em> (resolved in the
context of the coroutine) and to which <em>any</em> number of arguments
may be passed. Since every coroutine has a context command,
<strong>yieldto</strong> can be used to transfer control directly from
one coroutine to another (this is only advisable if the two coroutines
are expecting this to happen) but <em>any</em> command may be the
target. If a coroutine is suspended by this mechanism, the coroutine
processing can be resumed by calling the context command optionally
passing in an arbitrary number of arguments. The return value of the
<strong>yieldto</strong> call will be the list of arguments passed to
the context command; it is up to the caller to decide what to do with
those values.</p>
<p>The recommended way of writing a version of <strong>yield</strong>
that allows resumption with multiple arguments is by using
<strong>yieldto</strong> and the <strong>return</strong> command, like
this:</p>
<pre><code>proc yieldm {value} {
    yieldto return -level 0 $value
}</code></pre>
<p>The coroutine can also be deleted by destroying the command
<em>name</em>, and the name of the current coroutine can be retrieved by
using <strong>info coroutine</strong>. If there are deletion traces on
variables in the coroutine's implementation, they will fire at the point
when the coroutine is explicitly deleted (or, naturally, if the command
returns conventionally).</p>
<p>At the point when <em>command</em> is called, the current namespace
will be the global namespace and there will be no stack frames above it
(in the sense of <strong>upvar</strong> and <strong>uplevel</strong>).
However, which command to call will be determined in the namespace that
the <strong>coroutine</strong> command was called from.</p>
<h1 id="examples">EXAMPLES</h1>
<p>This example shows a coroutine that will produce an infinite sequence
of even values, and a loop that consumes the first ten of them.</p>
<pre><code>proc allNumbers {} {
    yield
    set i 0
    while 1 {
        yield $i
        incr i 2
    }
}
coroutine nextNumber allNumbers
for {set i 0} {$i &lt; 10} {incr i} {
    puts &quot;received [nextNumber]&quot;
}
rename nextNumber {}</code></pre>
<p>In this example, the coroutine acts to add up the arguments passed to
it.</p>
<pre><code>coroutine accumulator apply {{} {
    set x 0
    while 1 {
        incr x [yield $x]
    }
}}
for {set i 0} {$i &lt; 10} {incr i} {
    puts &quot;$i -&gt; [accumulator $i]&quot;
}</code></pre>
<p>This example demonstrates the use of coroutines to implement the
classic Sieve of Eratosthenes algorithm for finding prime numbers. Note
the creation of coroutines inside a coroutine.</p>
<pre><code>proc filterByFactor {source n} {
    yield [info coroutine]
    while 1 {
        set x [$source]
        if {$x % $n} {
            yield $x
        }
    }
}
coroutine allNumbers apply {{} {while 1 {yield [incr x]}}}
coroutine eratosthenes apply {c {
    yield
    while 1 {
        set n [$c]
        yield $n
        set c [coroutine prime$n filterByFactor $c $n]
    }
}} allNumbers
for {set i 1} {$i &lt;= 20} {incr i} {
    puts &quot;prime#$i = [eratosthenes]&quot;
}</code></pre>
<p>This example shows how a value can be passed around a group of three
coroutines that yield to each other:</p>
<pre><code>proc juggler {name target {value &quot;&quot;}} {
    if {$value eq &quot;&quot;} {
        set value [yield [info coroutine]]
    }
    while {$value ne &quot;&quot;} {
        puts &quot;$name : $value&quot;
        set value [string range $value 0 end-1]
        lassign [yieldto $target $value] value
    }
}
coroutine j1 juggler Larry [
    coroutine j2 juggler Curly [
        coroutine j3 juggler Moe j1]] &quot;Nyuck!Nyuck!Nyuck!&quot;</code></pre>
<h2 id="detailed-semantics">DETAILED SEMANTICS</h2>
<p>This example demonstrates that coroutines start from the global
namespace, and that <em>command</em> resolution happens before the
coroutine stack is created.</p>
<pre><code>proc report {where level} {
    # Where was the caller called from?
    set ns [uplevel 2 {namespace current}]
    yield &quot;made $where $level context=$ns name=[info coroutine]&quot;
}
proc example {} {
    report outer [info level]
}
namespace eval demo {
    proc example {} {
        report inner [info level]
    }
    proc makeExample {} {
        puts &quot;making from [info level]&quot;
        puts [coroutine coroEg example]
    }
    makeExample
}</code></pre>
<p>Which produces the output below. In particular, we can see that stack
manipulation has occurred (comparing the levels from the first and
second line) and that the parent level in the coroutine is the global
namespace. We can also see that coroutine names are local to the current
namespace if not qualified, and that coroutines may yield at depth
(e.g., in called procedures).</p>
<pre><code>making from 2
made inner 1 context=:: name=::demo::coroEg</code></pre>
<h1 id="see-also">SEE ALSO</h1>
<p><a href="apply.html">apply</a>, <a href="info.html">info</a>, <a
href="proc.html">proc</a>, <a href="return.html">return</a></p>
<h1 id="keywords">KEYWORDS</h1>
<p>coroutine, generator</p>
