<h1 id="name">NAME</h1>
<p>apply - Apply an anonymous function</p>
<h1 id="synopsis">SYNOPSIS</h1>
<p><strong>apply</strong> <em>func</em> ?<em>arg1 arg2 ...</em>?</p>
<h1 id="description">DESCRIPTION</h1>
<p>The command <strong>apply</strong> applies the function <em>func</em>
to the arguments <em>arg1 arg2 ...</em> and returns the result.</p>
<p>The function <em>func</em> is a two element list <em>{args body}</em>
or a three element list <em>{args body namespace}</em> (as if the
<strong>list</strong> command had been used). The first element
<em>args</em> specifies the formal arguments to <em>func</em>. The
specification of the formal arguments <em>args</em> is shared with the
<strong>proc</strong> command, and is described in detail in the
corresponding manual page.</p>
<p>The contents of <em>body</em> are executed by the Tcl interpreter
after the local variables corresponding to the formal arguments are
given the values of the actual parameters <em>arg1 arg2 ...</em>. When
<em>body</em> is being executed, variable names normally refer to local
variables, which are created automatically when referenced and deleted
when <strong>apply</strong> returns. One local variable is automatically
created for each of the function's arguments. Global variables can only
be accessed by invoking the <strong>global</strong> command or the
<strong>upvar</strong> command. Namespace variables can only be accessed
by invoking the <strong>variable</strong> command or the
<strong>upvar</strong> command.</p>
<p>The invocation of <strong>apply</strong> adds a call frame to Tcl's
evaluation stack (the stack of frames accessed via
<strong>uplevel</strong>). The execution of <em>body</em> proceeds in
this call frame, in the namespace given by <em>namespace</em> or in the
global namespace if none was specified. If given, <em>namespace</em> is
interpreted relative to the global namespace even if its name does not
start with</p>
<p>The semantics of <strong>apply</strong> can also be described by:</p>
<pre><code>proc apply {fun args} {
    set len [llength $fun]
    if {($len &lt; 2) || ($len &gt; 3)} {
         error &quot;can&#39;t interpret \&quot;$fun\&quot; as anonymous function&quot;
    }
    lassign $fun argList body ns
    set name ::$ns::[getGloballyUniqueName]
    set body0 {
         rename [lindex [info level 0] 0] {}
    }
    proc $name $argList ${body0}$body
    set code [catch {uplevel 1 $name $args} res opt]
    return -options $opt $res
}</code></pre>
<h1 id="examples">EXAMPLES</h1>
<p>This shows how to make a simple general command that applies a
transformation to each element of a list.</p>
<pre><code>proc map {lambda list} {
    set result {}
    foreach item $list {
        lappend result [apply $lambda $item]
    }
    return $result
}
map {x {return [string length $x]:$x}} {a bb ccc dddd}
      → 1:a 2:bb 3:ccc 4:dddd
map {x {expr {$x**2 + 3*$x - 2}}} {-4 -3 -2 -1 0 1 2 3 4}
      → 2 -2 -4 -4 -2 2 8 16 26</code></pre>
<p>The <strong>apply</strong> command is also useful for defining
callbacks for use in the <strong>trace</strong> command:</p>
<pre><code>set vbl &quot;123abc&quot;
trace add variable vbl write {apply {{v1 v2 op} {
    upvar 1 $v1 v
    puts &quot;updated variable to \&quot;$v\&quot;&quot;
}}}
set vbl 123
set vbl abc</code></pre>
<h1 id="see-also">SEE ALSO</h1>
<p><a href="proc.html">proc</a>, <a href="uplevel.html">uplevel</a></p>
<h1 id="keywords">KEYWORDS</h1>
<p>anonymous function, argument, lambda, procedure,</p>
