<h1 id="name">NAME</h1>
<p>eval - Evaluate a Tcl script</p>
<h1 id="synopsis">SYNOPSIS</h1>
<p><strong>eval</strong> <em>arg</em> ?<em>arg ...</em>?</p>
<h1 id="description">DESCRIPTION</h1>
<p><strong>Eval</strong> takes one or more arguments, which together
comprise a Tcl script containing one or more commands.
<strong>Eval</strong> concatenates all its arguments in the same fashion
as the <strong>concat</strong> command, passes the concatenated string
to the Tcl interpreter recursively, and returns the result of that
evaluation (or any error generated by it). Note that the
<strong>list</strong> command quotes sequences of words in such a way
that they are not further expanded by the <strong>eval</strong>
command.</p>
<h1 id="examples">EXAMPLES</h1>
<p>Often, it is useful to store a fragment of a script in a variable and
execute it later on with extra values appended. This technique is used
in a number of places throughout the Tcl core (e.g.Â in
<strong>fcopy</strong>, <strong>lsort</strong> and
<strong>trace</strong> command callbacks). This example shows how to do
this using core Tcl commands:</p>
<pre><code>set script {
    puts &quot;logging now&quot;
    lappend $myCurrentLogVar
}
set myCurrentLogVar log1
# Set up a switch of logging variable part way through!
after 20000 set myCurrentLogVar log2

for {set i 0} {$i&lt;10} {incr i} {
    # Introduce a random delay
    after [expr {int(5000 * rand())}]
    update    ;# Check for the asynch log switch
    eval $script $i [clock clicks]
}</code></pre>
<p>Note that in the most common case (where the script fragment is
actually just a list of words forming a command prefix), it is better to
use <strong>{*}$script</strong> when doing this sort of invocation
pattern. It is less general than the <strong>eval</strong> command, and
hence easier to make robust in practice. The following procedure acts in
a way that is analogous to the <strong>lappend</strong> command, except
it inserts the argument values at the start of the list in the
variable:</p>
<pre><code>proc lprepend {varName args} {
    upvar 1 $varName var
    # Ensure that the variable exists and contains a list
    lappend var
    # Now we insert all the arguments in one go
    set var [eval [list linsert $var 0] $args]
}</code></pre>
<p>However, the last line would now normally be written without
<strong>eval</strong>, like this:</p>
<pre><code>set var [linsert $var 0 {*}$args]</code></pre>
<h1 id="see-also">SEE ALSO</h1>
<p><a href="../Tcl-Commands/catch.html">catch</a>, <a
href="../Tcl-Commands/concat.html">concat</a>, <a
href="../Tcl-Commands/error.html">error</a>, <a
href="../Tcl-Commands/errorCode.html">errorCode</a>, <a
href="../Tcl-Commands/errorInfo.html">errorInfo</a>, <a
href="../Tcl-Commands/interp.html">interp</a>, <a
href="../Tcl-Commands/list.html">list</a>, <a
href="../Tcl-Commands/namespace.html">namespace</a>, <a
href="../Tcl-Commands/subst.html">subst</a>, <a
href="../Tcl-Commands/uplevel.html">uplevel</a></p>
<h1 id="keywords">KEYWORDS</h1>
<p>concatenate, evaluate, script</p>
